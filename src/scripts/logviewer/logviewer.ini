[script]
n0=
n1=alias logview {
n2=  logview.check
n3=  window -ael25k0z @Logview /logviewer.help
n4=  logviewer.help
n5=}
n6=
n7=alias logview.fill {
n8=  ;format: [-ns<d/n[+/-]>] [mask]
n9=  var %flags, %sort, %mask, %file, %i = 1, %time = $ctime
n10=  if ($left($1,1) == -) { set %flags $right($1,-1) | set %mask $2 $+ *.log | if (s isin $1) { set %sort $mid($1,$calc($pos($1,s,1) + 1),2) | set %flags $remove(%flags,s $+ %sort) } }
n11=  else { set %mask $1 $+ *.log | set %sort n+ }
n12=  echo %logview.info -t @logview Flags: $+ %logview.hl %flags Sort by: $+ %logview.hl %sort Mask: $+ %logview.hl %mask
n13=  var  %n = $findfile($logdir,%mask,0,2)
n14=  if (n isin %flags) %n = %n - $findfile($logdir,#* $+ %mask,0,2)
n15=  if ($findfile($logdir,*.log,0,2) == 0) { echo %logview.info @logview Логов в папке нету! Проверьте настройки mirc! | halt }
n16=  if (%n == 0) { echo %logview.info @logview По вашему запросу файлов не найдено! | halt }
n17=  echo %logview.info @logview -  
n18=  echo %logview.info @logview Files: $+ %logview.hl $findfile($logdir,%mask,0,2) Output: $+ %logview.hl %n
n19=  clear -l @Logview
n20=  while (%i <= $findfile($logdir,%mask,0,2)) {
n21=    %file = $nopath($findfile($logdir,%mask,%i,2))
n22=    if (#* iswm %file) { 
n23=      if (n !isin %flags) aline  -l %logview.chan @Logview %file 
n24=    }
n25=    else aline  -l %logview.nick @Logview %file
n26=    inc %i
n27=  }
n28=  echo %logview.info @logview Вывод файлов завершен. Затрачено $+ %logview.hl $calc($ctime - %time) секунд(а).
n29=  if ((%sort) && (%sort != n+)) logview.sort %sort
n30=}
n31=
n32=alias logview.sort {
n33=  var %i = 1, %m, %mn, %linei, %linej, %n = $line(@logview,0,1), %date, %time = $ctime, %name
n34=  if (!$istok(d+ d- n+ n-,$1,32)) { echo %logview.info @Logview Неизвестное свойство $+ %logview.hl $1 | halt }
n35=  echo %logview.info @logview -  
n36=  echo %logview.info -t @Logview Сортировка: $1  $+ %logview.hl $+ %n файла(ов).
n37=  if (%n >= 60) echo %logview.info @Logview Пожалуйста, подождите, сортировка может занять некоторое время, остальные окна работать не будут.
n38=  while (%i <= %n) {
n39=    if ($istok(d+ d-,$1,32)) %m = $file($logdir $+ $line(@Logview,%i,1)).mtime
n40=    if ($istok(n+ n-,$1,32)) %m = $line(@Logview,%i,1)
n41=    %mn = %i
n42=    %j = $calc(%i +1)
n43=    while (%j <= %n) {
n44=      %date = $file($logdir $+ $line(@Logview,%j,1)).mtime
n45=      %name = $line(@Logview,%j,1)
n46=      if ($1 == d-) { if (%date < %m) { %m = %date | %mn = %j } }
n47=      if ($1 == d+) { if (%date > %m) { %m = %date | %mn = %j  } }
n48=      if ($1 == n+) { if (%name < %m) { %m = %name | %mn = %j  } }
n49=      if ($1 == n-) { if (%name > %m) { %m = %name | %mn = %j  } }
n50=      inc %j
n51=    }
n52=    if (%mn != %i) {
n53=      %linei = $line(@Logview,%i,1)
n54=      %linej = $line(@Logview,%mn,1)
n55=      rline -l %logview. [ $+ [ $iif(#* iswm %linej,chan,nick) ] ] @Logview %i %linej
n56=      rline -l %logview. [ $+ [ $iif(#* iswm %linei,chan,nick) ] ] @Logview %mn %linei
n57=    }
n58=    inc %i
n59=  }
n60=  echo %logview.info @logview Сортировка закончена. Прошло: $+ %logview.hl $calc($ctime - %time) секунд(а)
n61=  halt
n62=}
n63=
n64=alias logview.filter {
n65=  echo %logview.info -t @logview Фильтр $line(@logview,0,1) файла(ов)
n66=  var %i = 1, %time = $ctime
n67=  if ($1 == s) {
n68=    echo %logview.info @logview Выделено $+ %logview.hl $iif($sline(@logview,0),$ifmatch,0) $iif($2 == -,останется $+ %logview.hl $calc($line(@logview,0,1) - $sline(@logview,0)) файл(ов))
n69=    while (%i <= $line(@logview,0,1)) {
n70=      if (($2 == +) && (!$line(@logview,%i,1).state)) { dline -l @logview %i | dec %i }
n71=      if (($2 == -) && ($line(@logview,%i,1).state)) { dline -l @logview %i | dec %i }
n72=      inc %i
n73=    }
n74=  }
n75=  if ($1 == m) {
n76=    echo %logview.info @logview Файлов $+ %logview.hl $line(@logview,0,1) Маска $+ %logview.hl $3-
n77=    while (%i <= $line(@logview,0,1)) {
n78=      if (($2 == +) && ($3- !iswm $line(@logview,%i,1))) { dline -l @logview %i | dec %i }
n79=      if (($2 == -) && ($3- iswm $line(@logview,%i,1))) { dline -l @logview %i | dec %i }
n80=      inc %i
n81=    }
n82=  }
n83=  if ($1 == f) {
n84=    echo %logview.info @logview Файлов $+ %logview.hl $line(@logview,0,1) Поиск в файлах строки $+ %logview.hl $2-
n85=    while (%i <= $line(@logview,0,1)) {
n86=      if (!$read($logdir $+ $line(@logview,%i,1),w,* $+ $2- $+ *,1)) { dline -l @logview %i | dec %i }
n87=      inc %i
n88=    }
n89=  }
n90=  echo %logview.info @logview Фильтрация завершена. Результат $+ %logview.hl $line(@logview,0,1) файл(ов). Затрачено $+ %logview.hl $calc($ctime - %time) секунд(а).
n91=}
n92=alias logview.fileinfo { return $input(Файл: $1 $+ $crlf $+ Строк: $lines($logdir $+ $1) $+ $crlf $+ Создан: $asctime($file($logdir $+ $1).ctime) $+ $crlf $+ Изменён: $asctime($file($logdir $+ $1).mtime),oi,Информация о файле) }
n93=alias logview.play {
n94=  var %flag, %file, %mod, %fn
n95=  echo -a $1-
n96=  if ($left($1,1) == -) { set %flag $right($1,-1) | set %file $logdir $+ $2 | set %fn $2 | set %mod $iif(%flag == m,* $+ $3- $+ *,$3-) }
n97=  else { set %file $logdir $+ $1 | set %fn $1 }
n98=  var %lines = $lines(%file), %i = 1, %j = 0, %n = 0
n99=  ;clear @logview
n100=  echo %logview.info -e @logview Файл %file (строк %lines $+ ): $iif($2,%flag %mod)
n101=  if ($fopen(logview)) .fclose logview
n102=  if (!$exists(" $+ %file $+ ")) { echo %logview.info -e @logview Ошибка: Файл не существует. | return }
n103=  .fopen logview " $+ %file $+ "
n104=  while (!$feof) {
n105=    set %string $fread(logview)
n106=    if (!%flag) {
n107=      echo @logview < $+ %i $+ > %string | inc %j
n108=    }
n109=    if ((%flag == s) && (%i isnum %mod)) {
n110=      echo @logview < $+ %i $+ > %string | inc %j
n111=    }
n112=    if ((%flag == m) && (%mod iswm %string)) {
n113=      echo @logview < $+ %i $+ > %string | inc %j
n114=    }
n115=    inc %i
n116=    if (%j > %logview.maxlines) {
n117=      .fclose logview
n118=      echo %logview.info @logview -
n119=      echo %logview.info @logview Вывод приостановлен. Выведено %j строк.
n120=      echo %logview.info @logview Для продолжения выберите в меню "Продолжить". 
n121=      echo %logview.info @logview -
n122=      %logview.continue = logview.play -s %fn %i $+ - 
n123=      return
n124=    }
n125=  }
n126=  .fclose logview
n127=  echo %logview.info -e @logview Вывод окончен. Выведено %j строк.
n128=}
n129=alias logview.continue {
n130=  var %cmd
n131=  %cmd = %logview.continue
n132=  unset %logview.continue
n133=  %cmd
n134=}
n135=
n136=alias logview.logdelete {
n137=  if (!%logview.delete) { if (!$input(Удалить $iif($sline(@logview,0) > 1,$ifmatch файла(ов)?,файл? $+ $crlf $+ $sline(@logview,1)),yq,Удаление логов)) return }
n138=  echo %logview.info -e @logview Удаление $sline(@logview,0) файла(ов).
n139=  while ($sline(@logview,0) > 0) {
n140=    remove $iif(!%logview.deltrash,-b) " $+ $logdir $+ $sline(@logview,1) $+ "
n141=    dline -l @logview $sline(@logview,1).ln
n142=  }
n143=  echo %logview.info -e @logview Удаление завершено. $iif(!%logview.deltrash,Вы можете восстановить удалённые файлы из корзины.)
n144=}
n145=
n146=
n147=menu menubar {
n148=  .-
n149=  .Логи
n150=  ..Просмотровщик:logview
n151=  ..Версии:run strings/logviewer.versions.txt
n152=  ..Настройки:logview.setup
n153=  .-
n154=}
n155=
n156=on *:load:{
n157=  logview.check
n158=  echo $iif(%logview.info isnum 0-15,$ifmatch) -a Magic Script Log Viewer loaded!
n159=}
n160=
n161=alias logview.check {
n162=  var %tmp, %info, %nick, %chan, %hl, %i = 0
n163=  var %theme = $readini(mirc.ini,text,theme)
n164=  while (%i < $ini(mirc.ini,colors,0)) {
n165=    set %tmp $readini(mirc.ini,colors,n $+ %i)
n166=    if ($gettok(%tmp,1,44) == %theme) { break }
n167=    inc %i
n168=  }
n169=  %logview.version = 0.8.9
n170=  if (%logview.info !isnum 0-15) {
n171=    if (!%logview.info) set %logview.info $iif(%colors.info isnum 0-15,$ifmatch,$str(0,$calc(2 - $len($gettok(%tmp,6,44)))) $+ $gettok(%tmp,6,44))
n172=    else %info = %logview.info
n173=  }
n174=  if (%logview.nick !isnum 0-15) {
n175=    if (!%logview.nick) set %logview.nick $iif(%colors.nick isnum 0-15,$ifmatch,$str(0,$calc(2 - $len($gettok(%tmp,12,44)))) $+ $gettok(%tmp,12,44))
n176=    else %nick = %logview.nick
n177=  }
n178=  if (%logview.chan !isnum 0-15) {
n179=    if (!%logview.chan) set %logview.chan $iif(%colors.chan isnum 0-15,$ifmatch,$str(0,$calc(2 - $len($gettok(%tmp,3,44)))) $+ $gettok(%tmp,3,44))
n180=    else %chan = %logview.chan
n181=  }
n182=  if (%logview.hl !isnum 0-15) {
n183=    if (!%logview.hl) set %logview.hl $iif(%colors.highlight isnum 0-15,$ifmatch,$str(0,$calc(2 - $len($gettok(%tmp,5,44)))) $+ $gettok(%tmp,5,44))
n184=    else %hl = %logview.hl
n185=  }
n186=  if ($numtok(%info %nick %chan %hl,32)) echo -ast Скрипт Magic Logviewer предпологает наличие переменных цвета. В случае их отсутствия возможны ошибки в работе.
n187=  if (%info) echo -ast %colors.info содержит значение  $+ %info $+  измените это значение на число 0-15 командой /set $chr(37) $+ logview.info число
n188=  if (%nick) echo -ast %colors.nick содержит значение  $+ %nick $+  измените это значение на число 0-15 командой /set $chr(37) $+ logview.nick число
n189=  if (%chan) echo -ast %colors.chan содержит значение  $+ %chan $+  измените это значение на число 0-15 командой /set $chr(37) $+ logview.chan число
n190=  if (%hl) echo -ast %colors.hl содержит значение  $+ %hl $+  измените это значение на число 0-15 командой /set $chr(37) $+ logview.hl число
n191=  if ($numtok(%info %nick %chan %hl,32)) echo -ast Если вы уверены, что никаких важных данных в этих переменных не содержится, можете ввести команду /logviewer.reset, которая установит цвета из вашей цветовой схемы по умолчанию.
n192=}
n193=
n194=alias logviewer.reset {
n195=  unset %logview.info
n196=  unset %logview.chan
n197=  unset %logview.nick
n198=  unset %logview.hl
n199=  logview.check
n200=}
n201=
n202=menu @logview {
n203=  dclick:{ %logview.mask = $$input(Введите маску,ey,Вывод по маске,%logview.mask) | logview.play -m $$sline($active,1) %logview.mask }
n204=  $iif(%logview.continue,Продолжить):logview.continue
n205=  -
n206=  Настройки:{ if ($dialog(logview.prefs)) dialog -v logview.prefs | else dialog -m logview.prefs prefs_logviewer }
n207=  Очистить окно вывода:clear @logview
n208=  Перегрузить список логов
n209=  .Полный:logview.fill
n210=  .Каналы:logview.fill $chr(35)
n211=  .Приваты:logview.fill -n
n212=  .Список по маске:{ %logview.outmask = $$input(Введите маску вывода: $crlf * - любая комбинация символов $crlf ? - один любой символ $crlf $+ в конце будет добавлено *.log,eyq,Маска вывода логов,%logview.outmask) | logview.fill %logview.outmask }
n213=  -
n214=  Сортировка
n215=  .По дате:logview.sort d-
n216=  .По дате обратно:logview.sort d+
n217=  .По имени:logview.sort n+
n218=  .По имени обратно:logview.sort n-
n219=  Фильтр
n220=  .Удалить по маске:{ %logview.filemask = $$input(Введите маску,ey,Фильтр логов,%logview.filemask) | logview.filter m - %logview.filemask }
n221=  .Оставить по маске:{ %logview.filemask = $$input(Введите маску,ey,Фильтр логов,%logview.filemask) | logview.filter m + %logview.filemask }
n222=  .Удалить выделенные:logview.filter s -
n223=  .Оставить выделенные:logview.filter s +
n224=  .Поиск строки в файле:{ %logview.searchmask = $$input(Введите маску,ey,Фильтр логов,%logview.searchmask) | logview.filter f %logview.searchmask }
n225=  Вывод
n226=  .$iif($1,Вывести полностью):logview.play $$1
n227=  .$iif($1,Вывести строки):logview.play -s $$1 $input(Введите номера строк,ey,Построчный вывод,1- $+ $lines($logdir $+ $1))
n228=  .$iif($1,Вывести по маске):{ %logview.mask = $$input(Введите маску,ey,Вывод по маске,%logview.mask) | logview.play -m $$1 %logview.mask }
n229=  $iif($1,Информация о $ifmatch):logview.fileinfo $1
n230=  -
n231=  Удаление файлов:logview.logdelete
n232=  -
n233=  Открыть папку с логами:run $logdir
n234=}
n235=
n236=alias logviewer.help {
n237=  var %ec echo %logview.info -h @Logview 
n238=  if ($1 == $null) {
n239=    %ec -
n240=    %ec Информация о Magic Logviewer %logview.version
n241=    %ec Скрипт позволяет просматривать файлы логов в окне mIRC сохраняя оригинальное форматирование и цвета.
n242=    %ec Для удобства предоставления информации используются четыре переменные цвета:
n243=    %ec $chr(9) $chr(37) $+ logview.nick $chr(9) ( $+ %logview.nick $+ ) $chr(9) - $chr(9) цвет файлов приватов
n244=    %ec $chr(9) $chr(37) $+ logview.chan $chr(9) ( $+ %logview.chan $+ ) $chr(9) - $chr(9) цвет файлов каналов
n245=    %ec $chr(9) $chr(37) $+ logview.info $chr(9) ( $+ %logview.info $+ ) $chr(9) - $chr(9) цвет информационных строк
n246=    %ec $chr(9) $chr(37) $+ logview.hl $chr(9) ( $+ %logview.hl $+ ) $chr(9) - $chr(9) цвет выделенной информации
n247=    %ec В скобках указано текущее значение цвета, сменить его можно командой /set $chr(37) $+ logview.nick <число>
n248=    %ec <Число> должно лежать в пределах 0-15 и являться номером цвета IRC - просмотреть их номера можно нажав Ctrl+K
n249=    %ec Для предотвращения ошибок отображения чисел старайтесь использовать двузначные номера цвета, например 04
n250=    echo @Logview 
n251=    %ec Для начала поиска необходимо перегрузить список логов (из меню, вызываемого правой клавишей мышки)
n252=    %ec Варианты загрузки:
n253=    %ec $chr(9) Полный $chr(9) - $chr(9) будут выведены все файлы логов (*.log)
n254=    %ec $chr(9) Каналы $chr(9) - $chr(9) будут выведены все файлы логов каналов (#*.log)
n255=    %ec $chr(9) Приваты $chr(9) - $chr(9) будут выведены все файлы логов приватов
n256=    %ec $chr(9) По маске $chr(9) - $chr(9) будут выведены все файлы логов, удовлетворяющие заданному запросу
n257=    %ec Например: a* выведет все логи приватов с никами, начинающимися на a, #*b выведет все логи каналов, содержащих в имени букву b
n258=    echo @Logview 
n259=    %ec Загруженный список логов можно отсортировать по имени и дате в возрастающем или убывающем порядке
n260=    echo @Logview 
n261=    %ec Список логов можно отфильтровать:
n262=    %ec $chr(9) По маске $chr(9) - $chr(9) будут удалены/оставлены файлы логов, удовлетворяющие заданной маске
n263=    %ec $chr(9) По выделеннию $chr(9) $chr(9) - $chr(9) будут удалены/оставлены файлы логов, выделенные мышкой (shift+mclick,ctrl+mclick)
n264=    %ec $chr(9) По строке$chr(9) - $chr(9) будут оставлены файлы логов, содержащие в себе хотя бы одну строку, удовлетворяющую маске (очень медленно)
n265=    echo @Logview 
n266=    %ec Вывести файл лога можно тремя способами:
n267=    %ec $chr(9) Полностью $chr(9) - $chr(9) будет выведен весь файл
n268=    %ec $chr(9) Строки $chr(9) - $chr(9) будут выведы строки, лежащие в заданном интервале (<начало>-<конец>); по умолчанию интервал принимает предельные значения (1-количество строк)
n269=    %ec $chr(9) По маске $chr(9) - $chr(9) будут выведы строки, удовлетворяющие заданной маске
n270=    %ec Для удобства поиска каждая строка предворяется своим номером.
n271=    return
n272=  }
n273=  if ($1 == menu) {
n274=    %ec -
n275=    %ec Настройки Magic Logviewer
n276=    %ec Цвета вывода должны быть числами от 0 до 15, посмотреть на таблицу цветов можно по нажатию ctrl+k.
n277=    %ec Так как желательно использование двухцифровых кодов, недостающий 0 будет автоматически прибавлен сскриптом.
n278=    %ec -
n279=    %ec Можно отключить подтверждения удаления и отключить удаление в корзину (не рекомендуется, так как восстановление файлов в этом случае сильно усложняется).
n280=    %ec Если при выводе файла строк оказывается больше предела, вывод останавливается и в строке ввода появляется команда продолжения вывода.
n281=    echo %logview.hl @Logview Пока данная возможность позволяет выводить файлы только по строкам. Непонятная ошибка не позволяет данному скрипту сработать второй раз подряд.
n282=    return
n283=  }
n284=  %ec -
n285=  %ec Топик $1 отсутствует.
n286=}
n287=
n288=dialog prefs_logviewer {
n289=  title "Настройки просмотровщика"
n290=  size -1 -1 150 68
n291=  option dbu
n292=  icon grafix\, 0
n293=  button "Применить", 1, 3 54 48 12, disable
n294=  button "Справка", 2, 51 54 48 12
n295=  button "Закрыть", 3, 99 54 48 12, ok
n296=  box "Цвета", 4, 1 1 50 50
n297=  text "Информация", 5, 3 9 32 8
n298=  text "Выделение", 6, 3 19 32 8
n299=  text "Ники", 7, 3 39 32 8
n300=  text "Каналы", 8, 3 29 32 8
n301=  edit "", 9, 37 8 10 10
n302=  edit "", 10, 37 18 10 10
n303=  edit "", 11, 37 28 10 10
n304=  edit "", 12, 37 38 10 10
n305=  check "Подтверждать удаление", 13, 65 8 80 10
n306=  check "Удалять в корзину", 14, 65 18 80 10
n307=  edit "", 15, 52 31 20 10
n308=  text "Предельное количество строк за один вывод", 16, 74 29 69 15
n309=}
n310=
n311=on *:dialog:logview.prefs:*:*: {
n312=  if ($devent == init) {
n313=    did -a $dname 9 %logview.info
n314=    did -a $dname 10 %logview.hl
n315=    did -a $dname 11 %logview.chan
n316=    did -a $dname 12 %logview.nick
n317=    if (!%logview.delete) did -c $dname 13
n318=    if (!%logview.deltrash) did -c $dname 14
n319=    did -a $dname 15 $iif(%logview.maxlines,$ifmatch,6000)
n320=    return
n321=  }
n322=  if ($devent == edit) {
n323=    did -e $dname 1
n324=  }
n325=  if ($devent == sclick) {
n326=    if ($did == 2) logviewer.help menu
n327=    if ($istok(13 14,$did,32)) did -e $dname 1
n328=    if ($did == 1) {
n329=      if ($did(9) isnum 0-15) %logview.info = $iif($len($did(9)) == 1,0 $+ $did(9),$did(9))
n330=      if ($did(10) isnum 0-15) %logview.hl = $iif($len($did(10)) == 1,0 $+ $did(10),$did(10))
n331=      if ($did(11) isnum 0-15) %logview.chan = $iif($len($did(11)) == 1,0 $+ $did(11),$did(11))
n332=      if ($did(12) isnum 0-15) %logview.nick = $iif($len($did(12)) == 1,0 $+ $did(12),$did(12))
n333=      if ($did(15) > 0) %logview.maxlines = $did(15)
n334=      if ($did(13)) %logview.delete = $false
n335=      if ($did(14)) %logview.deltrash = $false
n336=      did -b $dname 1
n337=    }
n338=  }
n339=}
