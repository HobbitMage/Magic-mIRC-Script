[script]
n0=
n1=alias logview {
n2=  logview.check
n3=  window -ael25k0z @Logview /logviewer.help
n4=  logviewer.help
n5=}
n6=
n7=alias logview.fill {
n8=  ;format: [-ns<d/n[+/-]>] [mask]
n9=  var %flags, %sort, %mask, %file, %i = 1, %time = $ctime
n10=  if ($left($1,1) == -) { set %flags $right($1,-1) | set %mask $2 $+ *.log | if (s isin $1) { set %sort $mid($1,$calc($pos($1,s,1) + 1),2) | set %flags $remove(%flags,s $+ %sort) } }
n11=  else { set %mask $1 $+ *.log | set %sort n+ }
n12=  echo %logview.info -t @logview Flags: $+ %logview.hl %flags Sort by: $+ %logview.hl %sort Mask: $+ %logview.hl %mask
n13=  var  %n = $findfile($logdir,%mask,0,2)
n14=  if (n isin %flags) %n = %n - $findfile($logdir,#* $+ %mask,0,2)
n15=  if ($findfile($logdir,*.log,0,2) == 0) { echo %logview.info @logview Логов в папке нету! Проверьте настройки mirc! | halt }
n16=  if (%n == 0) { echo %logview.info @logview По вашему запросу файлов не найдено! | halt }
n17=  echo %logview.info @logview Files: $+ %logview.hl $findfile($logdir,%mask,0,2) Output: $+ %logview.hl %n
n18=  clear -l @Logview
n19=  while (%i <= $findfile($logdir,%mask,0,2)) {
n20=    %file = $nopath($findfile($logdir,%mask,%i,2))
n21=    if (#* iswm %file) { 
n22=      if (n !isin %flags) aline  -l %logview.chan @Logview %file 
n23=    }
n24=    else aline  -l %logview.nick @Logview %file
n25=    inc %i
n26=  }
n27=  echo %logview.info @logview Вывод файлов завершен. Затрачено $+ %logview.hl $calc($ctime - %time) секунд(а).
n28=  if ((%sort) && (%sort != n+)) logview.sort %sort
n29=}
n30=
n31=alias logview.sort {
n32=  var %i = 1, %m, %mn, %linei, %linej, %n = $line(@logview,0,1), %date, %time = $ctime, %name
n33=  if (!$istok(d+ d- n+ n-,$1,32)) { echo %logview.info @Logview Неизвестное свойство $+ %logview.hl $1 | halt }
n34=  echo %logview.info -t @Logview Сортировка: $1  $+ %logview.hl $+ %n файла(ов).
n35=  if (%n >= 60) echo %logview.info @Logview Пожалуйста, подождите, сортировка может занять некоторое время, остальные окна работать не будут.
n36=  while (%i <= %n) {
n37=    inc %i
n38=  }
n39=  %i = 1
n40=  while (%i <= %n) {
n41=    if ($istok(d+ d-,$1,32)) %m = $file($logdir $+ $line(@Logview,%i,1)).mtime
n42=    if ($istok(n+ n-,$1,32)) %m = $line(@Logview,%i,1)
n43=    %mn = %i
n44=    %j = $calc(%i +1)
n45=    while (%j <= %n) {
n46=      %date = $file($logdir $+ $line(@Logview,%j,1)).mtime
n47=      %name = $line(@Logview,%j,1)
n48=      if ($1 == d-) { if (%date < %m) { %m = %date | %mn = %j } }
n49=      if ($1 == d+) { if (%date > %m) { %m = %date | %mn = %j  } }
n50=      if ($1 == n+) { if (%name < %m) { %m = %name | %mn = %j  } }
n51=      if ($1 == n-) { if (%name > %m) { %m = %name | %mn = %j  } }
n52=      inc %j
n53=    }
n54=    if (%mn != %i) {
n55=      %linei = $line(@Logview,%i,1)
n56=      %linej = $line(@Logview,%mn,1)
n57=      rline -l %logview. [ $+ [ $iif(#* iswm %linej,chan,nick) ] ] @Logview %i %linej
n58=      rline -l %logview. [ $+ [ $iif(#* iswm %linei,chan,nick) ] ] @Logview %mn %linei
n59=    }
n60=    inc %i
n61=  }
n62=  echo %logview.info @logview Сортировка закончена. Прошло: $+ %logview.hl $calc($ctime - %time) секунд(а)
n63=  halt
n64=}
n65=
n66=alias logview.filter {
n67=  echo %logview.info -t @logview Фильтр $line(@logview,0,1) файла(ов)
n68=  var %i = 1, %time = $ctime
n69=  if ($1 == s) {
n70=    echo %logview.info @logview Выделено $+ %logview.hl $iif($sline(@logview,0),$ifmatch,0) $iif($2 == -,останется $+ %logview.hl $calc($line(@logview,0,1) - $sline(@logview,0)) файл(ов))
n71=    while (%i <= $line(@logview,0,1)) {
n72=      if (($2 == +) && (!$line(@logview,%i,1).state)) { dline -l @logview %i | dec %i }
n73=      if (($2 == -) && ($line(@logview,%i,1).state)) { dline -l @logview %i | dec %i }
n74=      inc %i
n75=    }
n76=  }
n77=  if ($1 == m) {
n78=    echo %logview.info @logview Файлов $+ %logview.hl $line(@logview,0,1) Маска $+ %logview.hl $3-
n79=    while (%i <= $line(@logview,0,1)) {
n80=      if (($2 == +) && ($3- !iswm $line(@logview,%i,1))) { dline -l @logview %i | dec %i }
n81=      if (($2 == -) && ($3- iswm $line(@logview,%i,1))) { dline -l @logview %i | dec %i }
n82=      inc %i
n83=    }
n84=  }
n85=  if ($1 == f) {
n86=    echo %logview.info @logview Файлов $+ %logview.hl $line(@logview,0,1) Поиск в файлах строки $+ %logview.hl $2-
n87=    while (%i <= $line(@logview,0,1)) {
n88=      if (!$read($logdir $+ $line(@logview,%i,1),w,* $+ $2- $+ *,1)) { dline -l @logview %i | dec %i }
n89=      inc %i
n90=    }
n91=  }
n92=  echo %logview.info @logview Фильтрация завершена. Результат $+ %logview.hl $line(@logview,0,1) файл(ов). Затрачено $+ %logview.hl $calc($ctime - %time) секунд(а).
n93=}
n94=alias logview.fileinfo { return $input(Файл: $1 $+ $crlf $+ Строк: $lines($logdir $+ $1) $+ $crlf $+ Создан: $asctime($file($logdir $+ $1).ctime) $+ $crlf $+ Изменён: $asctime($file($logdir $+ $1).mtime),oi,Информация о файле) }
n95=alias logview.play {
n96=  var %flag, %file, %mod
n97=
n98=  if ($left($1,1) == -) { set %flag $right($1,-1) | set %file $logdir $+ $2 | set %mod $iif(%flag == m,* $+ $3- $+ *,$3-) }
n99=  else set %file $logdir $+ $1
n100=  var %lines = $lines(%file), %i = 1, %j = 0, %n = 0
n101=  ;clear @logview
n102=  echo %logview.info -e @logview Файл %file (строк %lines $+ ): $iif($2,%flag %mod)
n103=  if ($fopen(logview)) .fclose logview
n104=  if (!$exists(" $+ %file $+ ")) { echo %logview.info -e @logview Ошибка: Файл не существует. | return }
n105=  .fopen logview " $+ %file $+ "
n106=  while (!$feof) {
n107=    set %string $fread(logview)
n108=    if (!%flag) {
n109=      echo @logview < $+ %i $+ > %string | inc %j
n110=    }
n111=    if ((%flag == s) && (%i isnum %mod)) {
n112=      echo @logview < $+ %i $+ > %string | inc %j
n113=    }
n114=    if ((%flag == m) && (%mod iswm %string)) {
n115=      echo @logview < $+ %i $+ > %string | inc %j
n116=    }
n117=    inc %i
n118=  }
n119=  .fclose logview
n120=  echo %logview.info -e @logview Вывод окончен. Выведено %j строк.
n121=}
n122=
n123=alias logview.logdelete {
n124=  if (!%logview.delete) { if (!$input(Удалить $iif($sline(@logview,0) > 1,$ifmatch файла(ов)?,файл? $+ $crlf $+ $sline(@logview,1)),yq,Удаление логов)) return }
n125=  echo %logview.info -e @logview Удаление $sline(@logview,0) файла(ов).
n126=  while ($sline(@logview,0) > 0) {
n127=    remove $iif(!%logview.deltrash,-b) " $+ $logdir $+ $sline(@logview,1) $+ "
n128=    dline -l @logview $sline(@logview,1).ln
n129=  }
n130=  echo %logview.info -e @logview Удаление завершено. $iif(!%logview.deltrash,Вы можете восстановить удалённые файлы из корзины.)
n131=}
n132=
n133=
n134=menu menubar {
n135=  .-
n136=  .Логи
n137=  ..Просмотровщик:logview
n138=  ..Версии:run strings/logviewer.versions.txt
n139=  ..Настройки:logview.setup
n140=  .-
n141=}
n142=
n143=on *:load:{
n144=  logview.check
n145=  echo $iif(%logview.info isnum 0-15,$ifmatch) -a Magic Script Log Viewer loaded!
n146=}
n147=
n148=alias logview.check {
n149=  var %tmp, %info, %nick, %chan, %hl, %i = 0
n150=  var %theme = $readini(mirc.ini,text,theme)
n151=  while (%i < $ini(mirc.ini,colors,0)) {
n152=    set %tmp $readini(mirc.ini,colors,n $+ %i)
n153=    if ($gettok(%tmp,1,44) == %theme) { break }
n154=    inc %i
n155=  }
n156=  %logview.version = 0.9
n157=  if (%logview.info !isnum 0-15) {
n158=    if (!%logview.info) set %logview.info $iif(%colors.info isnum 0-15,$ifmatch,$str(0,$calc(2 - $len($gettok(%tmp,6,44)))) $+ $gettok(%tmp,6,44))
n159=    else %info = %logview.info
n160=  }
n161=  if (%logview.nick !isnum 0-15) {
n162=    if (!%logview.nick) set %logview.nick $iif(%colors.nick isnum 0-15,$ifmatch,$str(0,$calc(2 - $len($gettok(%tmp,12,44)))) $+ $gettok(%tmp,12,44))
n163=    else %nick = %logview.nick
n164=  }
n165=  if (%logview.chan !isnum 0-15) {
n166=    if (!%logview.chan) set %logview.chan $iif(%colors.chan isnum 0-15,$ifmatch,$str(0,$calc(2 - $len($gettok(%tmp,3,44)))) $+ $gettok(%tmp,3,44))
n167=    else %chan = %logview.chan
n168=  }
n169=  if (%logview.hl !isnum 0-15) {
n170=    if (!%logview.hl) set %logview.hl $iif(%colors.highlight isnum 0-15,$ifmatch,$str(0,$calc(2 - $len($gettok(%tmp,5,44)))) $+ $gettok(%tmp,5,44))
n171=    else %hl = %logview.hl
n172=  }
n173=  if ($numtok(%info %nick %chan %hl,32)) echo -ast Скрипт Magic Logviewer предпологает наличие переменных цвета. В случае их отсутствия возможны ошибки в работе.
n174=  if (%info) echo -ast %colors.info содержит значение  $+ %info $+  измените это значение на число 0-15 командой /set $chr(37) $+ logview.info число
n175=  if (%nick) echo -ast %colors.nick содержит значение  $+ %nick $+  измените это значение на число 0-15 командой /set $chr(37) $+ logview.nick число
n176=  if (%chan) echo -ast %colors.chan содержит значение  $+ %chan $+  измените это значение на число 0-15 командой /set $chr(37) $+ logview.chan число
n177=  if (%hl) echo -ast %colors.hl содержит значение  $+ %hl $+  измените это значение на число 0-15 командой /set $chr(37) $+ logview.hl число
n178=  if ($numtok(%info %nick %chan %hl,32)) echo -ast Если вы уверены, что никаких важных данных в этих переменных не содержится, можете ввести команду /logviewer.reset, которая установит цвета из вашей цветовой схемы по умолчанию.
n179=}
n180=
n181=alias logviewer.reset {
n182=  unset %logview.info
n183=  unset %logview.chan
n184=  unset %logview.nick
n185=  unset %logview.hl
n186=  logview.check
n187=}
n188=
n189=menu @logview {
n190=  dclick:{ %logview.mask = $$input(Введите маску,ey,Вывод по маске,%logview.mask) | logview.play -m $$sline($active,1) %logview.mask }
n191=  Настройки:{ if ($dialog(logview.prefs)) dialog -v logview.prefs | else dialog -m logview.prefs prefs_logviewer }
n192=  Очистить окно вывода:clear @logview
n193=  Перегрузить список логов
n194=  .Полный:logview.fill
n195=  .Каналы:logview.fill $chr(35)
n196=  .Приваты:logview.fill -n
n197=  .Список по маске:{ %logview.outmask = $$input(Введите маску вывода: $crlf * - любая комбинация символов $crlf ? - один любой символ $crlf $+ в конце будет добавлено *.log,eyq,Маска вывода логов,%logview.outmask) | logview.fill %logview.outmask }
n198=  -
n199=  Сортировка
n200=  .По дате:logview.sort d-
n201=  .По дате обратно:logview.sort d+
n202=  .По имени:logview.sort n+
n203=  .По имени обратно:logview.sort n-
n204=  Фильтр
n205=  .Удалить по маске:{ %logview.filemask = $$input(Введите маску,ey,Фильтр логов,%logview.filemask) | logview.filter m - %logview.filemask }
n206=  .Оставить по маске:{ %logview.filemask = $$input(Введите маску,ey,Фильтр логов,%logview.filemask) | logview.filter m + %logview.filemask }
n207=  .Удалить выделенные:logview.filter s -
n208=  .Оставить выделенные:logview.filter s +
n209=  .Поиск строки в файле:{ %logview.searchmask = $$input(Введите маску,ey,Фильтр логов,%logview.searchmask) | logview.filter f %logview.searchmask }
n210=  Вывод
n211=  .$iif($1,Вывести полностью):logview.play $$1
n212=  .$iif($1,Вывести строки):logview.play -s $$1 $input(Введите номера строк,ey,Построчный вывод,1- $+ $lines($logdir $+ $1))
n213=  .$iif($1,Вывести по маске):{ %logview.mask = $$input(Введите маску,ey,Вывод по маске,%logview.mask) | logview.play -m $$1 %logview.mask }
n214=  $iif($1,Информация о $ifmatch):logview.fileinfo $1
n215=  -
n216=  Удаление файлов:logview.logdelete
n217=  -
n218=  Открыть папку с логами:run $logdir
n219=}
n220=
n221=alias logviewer.help {
n222=  var %ec echo %logview.info -h @Logview 
n223=  if ($1 == $null) {
n224=    %ec -
n225=    %ec Информация о Magic Logviewer %logview.version
n226=    %ec Скрипт позволяет просматривать файлы логов в окне mIRC сохраняя оригинальное форматирование и цвета.
n227=    %ec Для удобства предоставления информации используются четыре переменные цвета:
n228=    %ec $chr(9) $chr(37) $+ logview.nick $chr(9) ( $+ %logview.nick $+ ) $chr(9) - $chr(9) цвет файлов приватов
n229=    %ec $chr(9) $chr(37) $+ logview.chan $chr(9) ( $+ %logview.chan $+ ) $chr(9) - $chr(9) цвет файлов каналов
n230=    %ec $chr(9) $chr(37) $+ logview.info $chr(9) ( $+ %logview.info $+ ) $chr(9) - $chr(9) цвет информационных строк
n231=    %ec $chr(9) $chr(37) $+ logview.hl $chr(9) ( $+ %logview.hl $+ ) $chr(9) - $chr(9) цвет выделенной информации
n232=    %ec В скобках указано текущее значение цвета, сменить его можно командой /set $chr(37) $+ logview.nick <число>
n233=    %ec <Число> должно лежать в пределах 0-15 и являться номером цвета IRC - просмотреть их номера можно нажав Ctrl+K
n234=    %ec Для предотвращения ошибок отображения чисел старайтесь использовать двузначные номера цвета, например 04
n235=    echo @Logview 
n236=    %ec Для начала поиска необходимо перегрузить список логов (из меню, вызываемого правой клавишей мышки)
n237=    %ec Варианты загрузки:
n238=    %ec $chr(9) Полный $chr(9) - $chr(9) будут выведены все файлы логов (*.log)
n239=    %ec $chr(9) Каналы $chr(9) - $chr(9) будут выведены все файлы логов каналов (#*.log)
n240=    %ec $chr(9) Приваты $chr(9) - $chr(9) будут выведены все файлы логов приватов
n241=    %ec $chr(9) По маске $chr(9) - $chr(9) будут выведены все файлы логов, удовлетворяющие заданному запросу
n242=    %ec Например: a* выведет все логи приватов с никами, начинающимися на a, #*b выведет все логи каналов, содержащих в имени букву b
n243=    echo @Logview 
n244=    %ec Загруженный список логов можно отсортировать по имени и дате в возрастающем или убывающем порядке
n245=    echo @Logview 
n246=    %ec Список логов можно отфильтровать:
n247=    %ec $chr(9) По маске $chr(9) - $chr(9) будут удалены/оставлены файлы логов, удовлетворяющие заданной маске
n248=    %ec $chr(9) По выделеннию $chr(9) $chr(9) - $chr(9) будут удалены/оставлены файлы логов, выделенные мышкой (shift+mclick,ctrl+mclick)
n249=    %ec $chr(9) По строке$chr(9) - $chr(9) будут оставлены файлы логов, содержащие в себе хотя бы одну строку, удовлетворяющую маске (очень медленно)
n250=    echo @Logview 
n251=    %ec Вывести файл лога можно тремя способами:
n252=    %ec $chr(9) Полностью $chr(9) - $chr(9) будет выведен весь файл
n253=    %ec $chr(9) Строки $chr(9) - $chr(9) будут выведы строки, лежащие в заданном интервале (<начало>-<конец>); по умолчанию интервал принимает предельные значения (1-количество строк)
n254=    %ec $chr(9) По маске $chr(9) - $chr(9) будут выведы строки, удовлетворяющие заданной маске
n255=    %ec Для удобства поиска каждая строка предворяется своим номером.
n256=    return
n257=  }
n258=  if ($1 == menu) {
n259=    %ec -
n260=    %ec Настройки Magic Logviewer
n261=    %ec Цвета вывода должны быть числами от 0 до 15, посмотреть на таблицу цветов можно по нажатию ctrl+k.
n262=    %ec Так как желательно использование двухцифровых кодов, недостающий 0 будет автоматически прибавлен сскриптом.
n263=    %ec -
n264=    %ec Можно отключить подтверждения удаления и отключить удаление в корзину (не рекомендуется, так как восстановление файлов в этом случае сильно усложняется).
n265=    %ec Если при выводе файла строк оказывается больше предела, вывод останавливается и в строке ввода появляется команда продолжения вывода.
n266=    %ec Пока невозможно вывести строки по маске и номеру строки, будет доступно в следующей версии.
n267=    return
n268=  }
n269=  %ec Топик $1 отсутствует.
n270=}
n271=
n272=dialog prefs_logviewer {
n273=  title "Настройки просмотровщика"
n274=  size -1 -1 150 68
n275=  option dbu
n276=  icon grafix\, 0
n277=  button "Применить", 1, 3 54 48 12, disable
n278=  button "Справка", 2, 51 54 48 12
n279=  button "Закрыть", 3, 99 54 48 12, ok
n280=  box "Цвета", 4, 1 1 50 50
n281=  text "Информация", 5, 3 9 32 8
n282=  text "Выделение", 6, 3 19 32 8
n283=  text "Ники", 7, 3 39 32 8
n284=  text "Каналы", 8, 3 29 32 8
n285=  edit "", 9, 37 8 10 10
n286=  edit "", 10, 37 18 10 10
n287=  edit "", 11, 37 28 10 10
n288=  edit "", 12, 37 38 10 10
n289=  check "Подтверждать удаление", 13, 65 8 80 10
n290=  check "Удалять в корзину", 14, 65 18 80 10
n291=  edit "", 15, 52 31 20 10
n292=  text "Предельное количество строк за один вывод", 16, 74 29 69 15
n293=}
n294=
n295=on *:dialog:logview.prefs:*:*: {
n296=  if ($devent == init) {
n297=    did -a $dname 9 %logview.info
n298=    did -a $dname 10 %logview.hl
n299=    did -a $dname 11 %logview.chan
n300=    did -a $dname 12 %logview.nick
n301=    if (!%logview.delete) did -c $dname 13
n302=    if (!%logview.deltrash) did -c $dname 14
n303=    did -a $dname 15 $iif(%logview.maxlines,$ifmatch,6000)
n304=    return
n305=  }
n306=  if ($devent == edit) {
n307=    did -e $dname 1
n308=  }
n309=  if ($devent == sclick) {
n310=    if ($did == 2) logviewer.help menu
n311=    if ($istok(13 14,$did,32)) did -e $dname 1
n312=    if ($did == 1) {
n313=      if ($did(9) isnum 0-15) %logview.info = $iif($len($did(9)) == 1,0 $+ $did(9),$did(9))
n314=      if ($did(10) isnum 0-15) %logview.hl = $iif($len($did(10)) == 1,0 $+ $did(10),$did(10))
n315=      if ($did(11) isnum 0-15) %logview.chan = $iif($len($did(11)) == 1,0 $+ $did(11),$did(11))
n316=      if ($did(12) isnum 0-15) %logview.nick = $iif($len($did(12)) == 1,0 $+ $did(12),$did(12))
n317=      if ($did(15) > 0) %logview.maxlines = $did(15)
n318=      if ($did(13)) %logview.delete = $false
n319=      if ($did(14)) %logview.deltrash = $false
n320=      did -b $dname 1
n321=    }
n322=  }
n323=}
