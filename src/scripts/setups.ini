[script]
n0=on *:start:proc.start
n1=on *:connect:proc.connect
n2=on *:disconnect:proc.disconnect
n3=on *:exit:proc.exit
n4=
n5=dialog user_menu {
n6=  title "Меню пользователя"
n7=  size 20 65 86 146
n8=  option dbu
n9=  icon grafix/user_menu.ico, 0
n10=  button "Закрыть", 1, 3 132 80 12, default ok
n11=  button "Справка", 2, 3 120 80 12
n12=  box "", 3, 1 -2 84 55
n13=  button "Настройки пользователя", 4, 3 3 80 12, default
n14=  button "Настройки обращения", 5, 3 15 80 12
n15=  button "Настройки ухода", 6, 3 27 80 12
n16=  button "Сообщения ухода", 7, 3 39 80 12
n17=  box "", 8, 1 49 84 43
n18=  button "Информация о скрипте", 9, 3 54 80 12
n19=  button "Настройки скрипта", 10, 3 66 80 12
n20=  button "Многосерверность", 11, 3 78 80 12
n21=  box "", 27, 1 88 84 31
n22=  button "Загрузить настройки", 25, 3 93 80 12
n23=  button "Сохранить настройки", 26, 3 105 80 12
n24=}
n25=
n26=on *:dialog:PMenu:*:*:{
n27=  if ($devent == sclick) {
n28=    if ($did == 2) { help.show prefs | return }
n29=    if ($did == 4) { 
n30=      if ($dialog(PUser)) dialog -vs PUser $calc($dialog(PMenu).x + $dialog(PMenu).w) $dialog(PMenu).y $dialog(PUser).cw $dialog(PUser).ch
n31=      else dialog -m PUser prefs_user 
n32=      return
n33=    }
n34=    if ($did == 5) { 
n35=      if ($dialog(Pnickins)) dialog -vs Pnickins $calc($dialog(PMenu).x + $dialog(PMenu).w) $dialog(PMenu).y $dialog(Pnickins).cw $dialog(Pnickins).ch
n36=      else dialog -m Pnickins prefs_nickins
n37=      return
n38=    }
n39=    if ($did == 6) { 
n40=      if ($dialog(Paway)) dialog -vs Paway $calc($dialog(PMenu).x + $dialog(PMenu).w) $dialog(PMenu).y $dialog(Paway).cw $dialog(Paway).ch
n41=      else dialog -m Paway prefs_away
n42=      return
n43=    }
n44=    if ($did == 7) { 
n45=      if ($dialog(Pawaymes)) dialog -vs Pawaymes $calc($dialog(PMenu).x + $dialog(PMenu).w) $dialog(PMenu).y $dialog(Pawaymes).cw $dialog(Pawaymes).ch
n46=      else dialog -m Pawaymes prefs_awaymes
n47=      return
n48=    }
n49=    if ($did == 9) { 
n50=      if ($dialog(sinfo)) dialog -vs sinfo $calc($dialog(PMenu).x + $dialog(PMenu).w) $dialog(PMenu).y $dialog(sinfo).cw $dialog(sinfo).ch
n51=      else dialog -m sinfo prefs_sinfo
n52=      return
n53=    }
n54=    if ($did == 10) { 
n55=      if ($dialog(Pscript)) dialog -vs Pscript $calc($dialog(PMenu).x + $dialog(PMenu).w) $dialog(PMenu).y $dialog(Pscript).cw $dialog(Pscript).ch
n56=      else dialog -m Pscript prefs_script
n57=      return
n58=    }
n59=    if ($did == 11) { 
n60=      if ($dialog(Pmserver)) dialog -vs Pmserver $calc($dialog(PMenu).x + $dialog(PMenu).w) $dialog(PMenu).y $dialog(Pmserver).cw $dialog(Pmserver).ch
n61=      else dialog -m Pmserver prefs_mserver
n62=      return
n63=    }
n64=    if ($did == 25) { 
n65=      if ($dialog(puser)) dialog -x puser 
n66=      if ($dialog(pnickins)) dialog -x pnickins
n67=      if ($dialog(paway)) dialog -x paway
n68=      if ($dialog(pawaymes)) dialog -x pawaymes
n69=      if ($dialog(sinfo)) dialog -x sinfo
n70=      if ($dialog(Pscript)) dialog -x Pscript
n71=      if ($dialog(Pmserver)) dialog -x Pmserver
n72=      proc.load
n73=    }
n74=    if ($did == 26) { if ($input(Не сохраняйте $+ $chr(44) если у вас появляются ошибки,qy,Сохранить все переменные?)) proc.save }
n75=  }
n76=}
n77=
n78=dialog prefs_user {
n79=  size $iif($calc($dialog(PMenu).x + $dialog(PMenu).w),$ifmatch,20) $iif($dialog(PMenu).y,$ifmatch,65) 150 82
n80=  option dbu
n81=  icon grafix\prefs_user.ico, 0
n82=  text "Основной ник", 12, 3 4 60 8
n83=  text "Альтернативный ник", 13, 3 15 60 8
n84=  text "Почтовый адрес", 14, 3 26 60 8
n85=  edit "", 4, 64 3 83 10
n86=  edit "", 5, 64 14 83 10
n87=  edit "", 6, 64 25 83 10, autohs
n88=  box "Пол", 7, 3 38 144 30
n89=  radio "Мужской", 8, 7 46 60 10
n90=  radio "Женский", 9, 7 56 60 10
n91=  radio "Неопределенный", 10, 77 46 60 10
n92=  radio "Множественный", 11, 77 56 60 10
n93=  button "Применить", 1, 3 69 48 12, disable
n94=  button "Справка", 2, 51 69 48 12
n95=  button "Закрыть", 3, 99 69 48 12, ok
n96=}
n97=
n98=on *:dialog:PUser:*:*:{
n99=  if ($devent == init) {
n100=    did -a PUser 4 $userget(nick)
n101=    did -a PUser 5 $userget(anick)
n102=    did -a PUser 6 $userget(pass.email)
n103=    if (%sex == m) { did -c PUser 8 | return }
n104=    if (%sex == f) { did -c PUser 9 | return }
n105=    if (%sex == u) { did -c PUser 10 | return }
n106=    if (%sex == n) { did -c PUser 11 | return }
n107=    did -c PUser 8 | did -e puser 12 
n108=  }
n109=  if ($devent == edit) {
n110=    did -e puser 1
n111=  }
n112=  if ($devent == sclick) {
n113=    if ($did == 2) {
n114=      help.show userprefs
n115=      return
n116=    }
n117=    if ($did == 1) { 
n118=      userset nick $did(4)
n119=      userset anick $did(5)
n120=      userset pass.email $did(6)
n121=      if ($did(8).state) userset sex m
n122=      if ($did(9).state) userset sex f
n123=      if ($did(10).state) userset sex u
n124=      if ($did(11).state) userset sex n
n125=      did -b puser 1
n126=      return
n127=    }
n128=    if ($istok(8 9 10 11,$did,32)) did -e puser 1
n129=  }
n130=}
n131=
n132=dialog prefs_nickins {
n133=  title "Настройки обращения"
n134=  size $iif($calc($dialog(PMenu).x + $dialog(PMenu).w),$ifmatch,20) $iif($dialog(PMenu).y,$ifmatch,65) 150 93
n135=  option dbu
n136=  icon grafix\prefs_nickins.ico, 0
n137=  button "Применить", 1, 39 80 36 12, disable
n138=  button "Справка", 2, 75 80 36 12
n139=  button "Закрыть", 3, 111 80 36 12, ok
n140=  box "Способ обращения при пустой/заполненной строке", 4, 1 1 148 58
n141=  edit "", 5, 2 60 146 10
n142=  edit "", 6, 2 69 146 10, read
n143=  radio "Приват/Обращение", 7, 3 8 65 10
n144=  radio "Подстановка/Обращение", 8, 71 8 77 10
n145=  radio "Подстановка/Приват", 9, 3 17 65 10
n146=  radio "Приват/Подстановка", 10, 71 17 77 10
n147=  text "Приват - Открыть приват с выбранным ником.", 11, 3 27 144 8
n148=  text "Подстановка - В строке ввода текст изменяется, но не отправляется", 12, 3 43 144 13
n149=  text "Обращение - Отправляется строка обращения", 13, 3 35 144 8
n150=  button "Стандартная", 14, 2 80 37 12
n151=}
n152=
n153=on *:dialog:Pnickins:*:*:{
n154=  if ($devent == init) {
n155=    did -a Pnickins 5 %str.nickins
n156=    var %nick = $me, %mes = строка ввода
n157=    did -a Pnickins 6 [ [ %str.nickins ] ]
n158=    if (%prefs.nickinsert.type !isnum 1-4) { %prefs.nickinsert.type = 1 | did -e Pnickins 1 }
n159=    did -c Pnickins $calc(6 + %prefs.nickinsert.type) 
n160=  }
n161=  if ($devent == edit) {
n162=    did -e Pnickins 1
n163=  }
n164=  if ($devent == sclick) {
n165=    if ($did == 2) {
n166=      help.show nickinsert
n167=      return
n168=    }
n169=    if ($did == 1) {
n170=      %str.nickins = $did(5)
n171=      if ($did(7).state) %prefs.nickinsert.type = 1
n172=      if ($did(8).state) %prefs.nickinsert.type = 2
n173=      if ($did(9).state) %prefs.nickinsert.type = 3
n174=      if ($did(10).state) %prefs.nickinsert.type = 4
n175=      did -b pnickins 1
n176=      return
n177=    }
n178=    if ($istok(7 8 9 10,$did,32)) { did -e Pnickins 1 | return }
n179=    if ($did == 14) { did -o pnickins 5 1 $stdget(str.nickins) | did -e Pnickins 1 | return }
n180=  }
n181=}
n182=
n183=dialog prefs_away {
n184=  title "Настройки ухода"
n185=  size $iif($calc($dialog(PMenu).x + $dialog(PMenu).w),$ifmatch,20) $iif($dialog(PMenu).y,$ifmatch,65) 150 92
n186=  option dbu
n187=  icon grafix\prefs_away.ico, 0
n188=  button "Применить", 1, 3 79 48 12, disable
n189=  button "Справка", 2, 51 79 48 12
n190=  button "Закрыть", 3, 99 79 48 12, ok
n191=  box "Смена ника", 4, 1 0 47 67
n192=  radio "Не менять", 5, 4 8 40 10, group
n193=  radio "Весь ник", 6, 4 18 40 10
n194=  radio "Приставка", 7, 4 28 40 10
n195=  radio "Окончание", 8, 4 38 40 10
n196=  radio "Приставка и окончание", 9, 4 48 40 10
n197=  text "Ник", 11, 50 4 25 8
n198=  text "Приставка", 12, 50 25 38 8
n199=  text "окончание", 10, 14 57 41 8
n200=  text "Окончание", 13, 50 46 39 8
n201=  text "Результат", 14, 2 69 27 8
n202=  box "", 15, 48 0 101 67
n203=  edit "", 16, 50 13 97 10
n204=  edit "", 17, 50 34 97 10
n205=  edit "", 18, 50 55 97 10
n206=  edit "", 19, 29 68 120 10, disable
n207=}
n208=
n209=alias dialog.paway.create {
n210=  if ($did(5).state) return $me
n211=  if ($did(6).state) return  $did(16)
n212=  if ($did(7).state) return  $did(17) $+ $me
n213=  if ($did(8).state) return  $me $+ $did(18)
n214=  if ($did(9).state) return  $did(17) $+ $me $+ $did(18)
n215=}
n216=on *:dialog:Paway:*:*:{
n217=  if ($devent == init) {
n218=    did -c paway $calc(5 + %prefs.away.nickmode)
n219=    did -a paway 16 %str.away.nick
n220=    did -a paway 17 %str.away.pre
n221=    did -a paway 18 %str.away.end
n222=    did -o paway 19 1 $dialog.paway.create
n223=    return
n224=  }
n225=  if ($devent == edit) {
n226=    did -e Paway 1
n227=    did -o paway 19 1 $dialog.paway.create
n228=    return
n229=  }
n230=  if ($devent == sclick) {
n231=    if ($did == 2) {
n232=      help.show maway
n233=      return
n234=    }
n235=    if ($did == 1) {
n236=      did -b Paway 1
n237=      %str.away.nick = $did(16)
n238=      %str.away.pre = $did(17)
n239=      %str.away.end = $did(18)
n240=      if ($did(5).state) %prefs.away.nickmode = 0
n241=      if ($did(6).state) %prefs.away.nickmode = 1
n242=      if ($did(7).state) %prefs.away.nickmode = 2
n243=      if ($did(8).state) %prefs.away.nickmode = 3
n244=      if ($did(9).state) %prefs.away.nickmode = 4
n245=      return
n246=    }
n247=    if ($istok(5 6 7 8 9,$did,32)) { did -e Paway 1 | did -o paway 19 1 $dialog.paway.create | return }
n248=  }
n249=}
n250=
n251=dialog prefs_awaymes {
n252=  title "Сообщения ухода"
n253=  size $iif($calc($dialog(PMenu).x + $dialog(PMenu).w),$ifmatch,20) $iif($dialog(PMenu).y,$ifmatch,65) 150 146
n254=  option dbu
n255=  icon grafix\prefs_awaymes.ico, 0
n256=  button "Применить", 1, 3 133 48 12, disable
n257=  button "Справка", 2, 51 133 48 12
n258=  button "Закрыть", 3, 99 133 48 12, ok
n259=  box "Настройки", 4, 1 0 148 37
n260=  check "Включены", 5, 3 6 50 10
n261=  check "Промежуточные", 6, 3 16 53 10, disable
n262=  radio "Сообщения", 7, 96 6 50 10, disable left
n263=  radio "Действия", 8, 96 16 50 10, disable left
n264=  radio "Нотисы", 9, 96 26 50 10, disable left
n265=  edit "", 10, 25 26 15 9, disable autohs
n266=  text "мин.", 11, 41 26 13 8, disable
n267=  text "Каждые", 12, 3 26 21 8, disable
n268=  text "Сообщение об уходе", 13, 2 38 104 8
n269=  button "Стандарт", 15, 110 37 38 9
n270=  edit "", 14, 2 46 147 10, autohs
n271=  text "Сообщение о возвращении", 16, 2 57 104 8
n272=  edit "", 17, 2 65 147 10, autohs
n273=  button "Стандарт", 18, 110 56 38 9
n274=  text "Сообщение о смене причины", 19, 2 76 104 8
n275=  edit "", 20, 2 84 147 10, autohs
n276=  button "Стандарт", 21, 110 75 38 9
n277=  text "Промежуточное сообщение", 22, 2 95 104 8
n278=  edit "", 23, 2 103 147 10, autohs
n279=  button "Стандарт", 24, 110 94 38 9
n280=  text "Неизменная причина", 25, 2 114 104 8
n281=  edit "", 26, 2 122 147 10, autohs
n282=  button "Стандарт", 27, 110 113 38 9
n283=}
n284=
n285=on *:dialog:Pawaymes:*:*:{
n286=  if ($devent == init) {
n287=    did -a pawaymes 14 %str.away.msg
n288=    did -a pawaymes 17 %str.away.return
n289=    did -a pawaymes 20 %str.away.change
n290=    did -a pawaymes 23 %str.away.repeat
n291=    did -a pawaymes 26 %str.away.nochange
n292=    if (%prefs.away.mes) { did -c pawaymes 5 | did -e pawaymes 6-12 }
n293=    if (%prefs.away.mesmid) did -c pawaymes 6
n294=    if (%prefs.away.mesmode == describe) did -c pawaymes 8
n295=    if (%prefs.away.mesmode == notice) did -c pawaymes 9
n296=    if ((!$did(8).state) && (!$did(9).state)) did -c pawaymes 7
n297=    did -a pawaymes 10 %nums.away.rep.mins
n298=  }
n299=  if ($devent == edit) {
n300=    did -e Pawaymes 1 
n301=  }
n302=  if ($devent == sclick) {
n303=    if ($did == 5) { if (!$did(5).state) did -b pawaymes 6-12 | else did -e pawaymes 6-12 }
n304=    if ($did == 2) {
n305=      help.show mawaymes
n306=      return
n307=    }
n308=    if ($did == 1) {
n309=      %prefs.away.mes = $iif($did(5).state,$true,$false)
n310=      %prefs.away.mesmid = $iif($did(6).state,$true,$false)
n311=      if ($did(7).state) %prefs.mesmode = msg
n312=      if ($did(8).state) %prefs.mesmode = describe
n313=      if ($did(9).state) %prefs.mesmode = notice
n314=      if ($did(10) !isnum 1-999) { did -o pawaymes 10 1 $iif(%nums.away.rep.mins,$ifmatch,$stdget(nums.away.rep.mins)) }
n315=      %nums.away.rep.mins = $did(10)
n316=      did -b Pawaymes 1
n317=      %str.away.msg = $did(14)
n318=      %str.away.return = $did(17)
n319=      %str.away.change = $did(20)
n320=      %str.away.repeat = $did(23)
n321=      %str.away.nochange = $did(26)
n322=      return
n323=    }
n324=    if ($istok(5 6 7 8 9,$did,32)) { did -e Pawaymes 1 | return }
n325=    if ($did == 15) {
n326=      did -e pawaymes 1
n327=      did -o pawaymes 14 1 %str.away.msg
n328=      return
n329=    }
n330=    if ($did == 18) {
n331=      did -e pawaymes 1
n332=      did -o pawaymes 17 1 %str.away.return
n333=      return
n334=    }
n335=    if ($did == 21) {
n336=      did -e pawaymes 1
n337=      did -o pawaymes 20 1 %str.away.change
n338=      return
n339=    }
n340=    if ($did == 24) {
n341=      did -e pawaymes 1
n342=      did -o pawaymes 23 1 %str.away.repeat
n343=      return
n344=    }
n345=    if ($did == 27) {
n346=      did -e pawaymes 1
n347=      did -o pawaymes 26 1 %str.away.nochange
n348=      return
n349=    }
n350=  }
n351=}
n352=
n353=dialog prefs_sinfo {
n354=  title "Информация о скрипте"
n355=  size $iif($calc($dialog(PMenu).x + $dialog(PMenu).w),$ifmatch,20) $iif($dialog(PMenu).y,$ifmatch,65) 150 47
n356=  option dbu
n357=  icon grafix\prefs_sinfo.ico, 0
n358=  button "Применить", 1, 3 34 48 12, hide disable
n359=  button "Справка", 2, 51 34 48 12
n360=  button "Закрыть", 3, 99 34 48 12, ok
n361=  text "Скрипт", 4, 1 2 20 8
n362=  edit "", 5, 21 1 128 10, read autohs
n363=  text "Версия", 6, 1 13 20 8
n364=  edit "", 7, 21 12 128 10, read autohs
n365=  text "Дата", 8, 1 24 20 8
n366=  edit "", 9, 21 23 128 10, read autohs
n367=}
n368=
n369=on *:dialog:sinfo:*:*:{
n370=  if ($devent == sclick) { if ($did == 2) {
n371=      help.show msinfo
n372=      return
n373=  } }
n374=  if ($devent == init) {
n375=    did -a sinfo 5 %str.scriptname
n376=    did -a sinfo 7 %str.version
n377=    did -a sinfo 9 %str.vdate
n378=  }
n379=}
n380=
n381=dialog prefs_script {
n382=  title "Настройки скрипта"
n383=  size $iif($calc($dialog(PMenu).x + $dialog(PMenu).w),$ifmatch,20) $iif($dialog(PMenu).y,$ifmatch,65) 235 114
n384=  option dbu
n385=  icon grafix\prefs_script, 0
n386=  button "Применить", 1, 3 100 48 12, disable
n387=  button "Справка", 2, 51 100 48 12
n388=  button "Закрыть", 3, 99 100 48 12, ok
n389=  button "Статус", 4, 2 1 31 9
n390=  edit "", 5, 34 1 199 10, autohs
n391=  button "Сервис", 6, 2 12 31 9
n392=  edit "", 7, 34 12 199 10, autohs
n393=  button "Выход", 8, 2 23 31 9
n394=  edit "", 9, 34 23 199 10, autohs
n395=  button "Finger", 10, 2 34 31 9
n396=  edit "", 11, 34 34 199 10, autohs
n397=  check "Расширенный whois", 12, 2 45 57 10
n398=  button "Формат времени", 13, 60 45 46 10
n399=  edit "", 14, 106 45 64 10, return autohs center
n400=  edit "", 15, 169 45 64 10, disable center
n401=  check "Автозапуск часов", 16, 4 65 56 10
n402=  check "Отдельное окно", 17, 60 65 50 10
n403=  text "Координаты:", 18, 134 67 33 8
n404=  edit "", 19, 169 65 62 10, right
n405=  box "Часы", 20, 2 57 231 21
n406=  text "Количество символов в строках меню", 21, 2 80 98 8
n407=  edit "", 22, 101 79 16 10, center
n408=  check "Показывать справку при запуске", 23, 139 90 93 10, left
n409=  check "Отображать окно сервисов", 24, 3 90 114 10
n410=}
n411=
n412=alias pscript.tik { did -o pscript 15 1 $asctime($did(pscript,14)) }
n413=on *:dialog:Pscript:*:*:{
n414=  if ($devent == init) {
n415=    did -a pscript 5 %str.status.click
n416=    did -a pscript 7 %str.servecho
n417=    did -a pscript 9 %str.quit.mes
n418=    did -a pscript 11 %str.finger.ans
n419=    if (%prefs.whois.show.idle) did -c pscript 12
n420=    did -a pscript 14 $userget(timestamp)
n421=    pscript.tik
n422=    .timerpscript -o 0 1 pscript.tik
n423=    if (%prefs.clock) did -c pscript 16
n424=    if (%prefs.clock.window) did -c pscript 17
n425=    did -a pscript 19 %prefs.clock.xy
n426=    did -a pscript 22 %nums.menulist.len
n427=    if (%prefs.help.autostart) did -c pscript 23
n428=    if (%prefs.serv.window) did -c pscript 24
n429=  }
n430=  if ($devent == edit) {
n431=    did -e Pscript 1
n432=  }
n433=  if ($devent == sclick) {
n434=    if ($did == 2) {
n435=      help.show mscript
n436=      return
n437=    }
n438=    if ($did == 1) {
n439=      %str.status.click = $did(5)
n440=      %str.servecho = $did(7)
n441=      %str.quit.mes = $did(9)
n442=      %str.finger.ans = $did(11)
n443=      %prefs.whois.show.idle = $did(12).state
n444=      userset timestamp $did(14)
n445=      %prefs.clock = $did(16).state
n446=      %prefs.clock.window = $did(17).state
n447=      %prefs.help.autostart = $did(23).state
n448=      %prefs.serv.window = $did(24).state
n449=      %prefs.clock.xy = $did(19)
n450=      %nums.menulist.len = $iif($did(22) isnum 5-50,$ifmatch,$stdget(nums.menulist.len))
n451=      if ($did(22) !isnum 5-50) did -o pscript 22 1 $stdget(nums.menulist.len)
n452=      did -b Pscript 1
n453=      return
n454=    }
n455=    if ($istok(4 6 8 10 13 16 17 23 24,$did,32)) { did -e Pscript 1 }
n456=    if ($did == 4) did -o pscript 5 1 $stdget(str.status.click)
n457=    if ($did == 6) did -o pscript 7 1 $stdget(str.servecho)
n458=    if ($did == 8) did -o pscript 9 1 $stdget(str.quit.mes)
n459=    if ($did == 10) did -o pscript 11 1 $stdget(str.finger.ans)
n460=    if ($did == 13) did -o pscript 14 1 $stdget(user.timestamp)
n461=  }
n462=  if ($devent == close) { .timerpscript off }
n463=}
n464=
n465=dialog prefs_mserver {
n466=  title "Многосерверность"
n467=  size $iif($calc($dialog(PMenu).x + $dialog(PMenu).w),$ifmatch,20) $iif($dialog(PMenu).y,$ifmatch,65) 250 54
n468=  option dbu
n469=  icon grafix\prefs_mserver.ico, 0
n470=  button "Применить", 1, 62 41 48 12, disable
n471=  button "Справка", 2, 110 41 48 12
n472=  button "Закрыть", 3, 158 41 48 12, ok
n473=  check "Автосоединение с серверами по списку:", 4, 1 1 148 10
n474=  edit "", 5, 1 11 247 10
n475=  button "Добавить текущий", 6, 2 41 55 12
n476=  box "Команды меню, работающие для всех серверов", 7, 4 22 244 17
n477=  check "Смена ника", 8, 7 28 47 10
n478=  check "Режим ухода", 9, 57 28 50 10
n479=  check "Выход из сети", 10, 111 28 50 10
n480=}
n481=
n482=on *:dialog:Pmserver:*:*:{
n483=  if ($devent == init) {
n484=    if (%prefs.multiserver) did -c pmserver 4
n485=    did -a pmserver 5 %prefs.servers
n486=    if (%prefs.mserver.nick) did -c pmserver 8
n487=    if (%prefs.mserver.away) did -c pmserver 9
n488=    if (%prefs.mserver.quit) did -c pmserver 10
n489=  }
n490=  if ($devent == edit) {
n491=    did -e Pmserver 1
n492=  }
n493=  if ($devent == sclick) {
n494=    if ($did == 2) {
n495=      help.show mserver
n496=      return
n497=    }
n498=    if ($did == 1) {
n499=      if ($did(4).state) %prefs.multiserver = $true
n500=      else %prefs.multiserver = $false
n501=      if ($did(8).state) %prefs.mserver.nick = $true
n502=      else %prefs.mserver.nick = $false
n503=      if ($did(9).state) %prefs.mserver.away = $true
n504=      else %prefs.mserver.away = $false
n505=      if ($did(10).state) %prefs.mserver.quit = $true
n506=      else %prefs.mserver.quit = $false
n507=      %prefs.servers = $did(5)
n508=      did -b Pmserver 1
n509=      return
n510=    }
n511=    if ($istok(4 8 9 10,$did,32)) { did -e pmserver 1 | return }
n512=    if ($did == 6) { scid $activecid did -o pmserver 5 1 $addtok($did(5),$server,32) | did -e Pmserver 1 | return }
n513=  }
n514=}
n515=
n516=dialog prefs_sounds {
n517=  title "Настройки звуков"
n518=  size $iif($calc($dialog(PMenu).x + $dialog(PMenu).w),$ifmatch,20) $iif($dialog(PMenu).y,$ifmatch,65) 150 95
n519=  option dbu
n520=  icon grafix\prefs_sounds.ico, 0
n521=  button "Применить", 1, 3 80 48 12, disable
n522=  button "Справка", 2, 51 80 48 12
n523=  button "Закрыть", 3, 99 80 48 12, ok
n524=}
n525=
n526=
n527=dialog clock {
n528=  title "Часы"
n529=  size $iif(%prefs.clock.xy,$ifmatch,-1 -1) 50 8
n530=  option dbu
n531=  text "", 1, 0 0 50 10, center
n532=}
n533=
n534=on *:dialog:Clock:*:*:{
n535=  if ($devent == init) {
n536=    did -a Clock 1 [ $timestamp ]
n537=    .timerclock -o 0 1 clock.tik
n538=  }
n539=  if ($devent == close) { .timerclock off }
n540=  if ($devent == rclick) { if ($input(Сохранить положение часов?,yq,Положение часов)) set %prefs.clock.xy $dialog(clock).x $dialog(clock).y }
n541=}
n542=
n543=alias clock.tik { did -o clock 1 1 $timestamp }
